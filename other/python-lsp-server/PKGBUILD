# Maintainer: <archlinux@vale.re>

pkgname=python-lsp-server
pkgver=1.0.1
pkgrel=2
pkgdesc='An implementation of the Language Server Protocol for Python'
arch=(any)
url='https://github.com/python-lsp/python-lsp-server'
license=(MIT)
depends=('python>=3.6' 'python-jedi>=0.17.2' 'python-jedi<0.19.0' 'python-lsp-jsonrpc>=1.0.0' 'python-pluggy' 'python-ujson>=3.0.0')
makedepends=('python-setuptools>=39.0.0')
optdepends=('autopep8: for code formatting'
            'flake8: for code linting'
            'python-lsp-black: for code formatting'
            'python-mccabe: for complexity checking'
            'python-pycodestyle: for style checking'
            'python-pydocstyle: for docstring style checking'
            'python-pyflakes: for linter to detect various errors'
            'python-pylint: for code linting'
            'python-rope: for completions and renaming'
            'yapf: for code formatting')
checkdepends=(autopep8 flake8 python-mccabe python-pycodestyle python-pydocstyle
              python-pyflakes python-pylint python-rope yapf
              python-pytest python-pytest-cov python-coverage python-numpy
              python-pandas python-matplotlib python-flaky)
source=("https://github.com/python-lsp/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('b049d9a02cc568814d56278df5e95bcce455182ba7115dcd76b3212ef80f278f')

build() {
    cd ${pkgname}-${pkgver}
    python setup.py build
}

check() {
    cd ${pkgname}-${pkgver}
    # TODO: fix 2 failing cases in pylint
    py.test -k 'not test_pyqt_completion and not test_pylint'
}

package() {
    cd ${pkgname}-${pkgver}
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
