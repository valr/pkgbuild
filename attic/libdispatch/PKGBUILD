pkgname=libdispatch
pkgver=5.4
pkgrel=1
pkgdesc='Comprehensive support for concurrent code execution on multicore hardware'
arch=('x86_64')
url='https://apple.github.io/swift-corelibs-libdispatch'
license=('Apache')
makedepends=('git' 'clang' 'cmake')
source=("${pkgname}::git+https://github.com/apple/swift-corelibs-libdispatch.git#tag=swift-${pkgver}-RELEASE"
        "remove-werror.patch")
sha512sums=('SKIP'
            '0ab59eae72dd9f2462ced91958421f6d5dc0ff94bd414fb4f0416e85325ef32408e702ab0bfb0f41a98a21fa1f5ceec7b3f53a8f9c6c8d59882f12461bd7c2c4')

prepare() {
    cd "$pkgname"
    patch -p0 < "${srcdir}/remove-werror.patch"
}

build () {
    CC=clang CXX=clang++ cmake -S"${pkgname}" -Bbuild \
        -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBlocksRuntime_INCLUDE_DIR=/usr/include \
        -DBlocksRuntime_LIBRARIES=/usr/lib/libBlocksRuntime.so
    cmake --build build -j $(nproc)
}

check () {
    cmake --build build --target test
}

package () {
    DESTDIR="${pkgdir}" cmake --install build
}
